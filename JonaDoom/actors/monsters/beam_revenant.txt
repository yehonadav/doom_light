ACTOR BeamRevenant : Revenant 2112
{
	//$Category JonaDoom_Monsters
  HitObituary "%o was punched by a Beam Revenant."
  Obituary "%o couldn't was caught in a Beam Revenant's crosshairs."
  States
  {
  Spawn:
    RASK AB 10 A_Look
    Loop
  See:
    RASK AAABBBCCCDDDEEEFFF 2 A_Chase
    Loop
  Melee:
    RASK G 1 A_FaceTarget
    RASK G 6 A_SkelWhoosh
    RASK H 6 A_FaceTarget
    RASK I 6 A_SkelFist
    Goto See
  Missile:
    RASK J 1 Bright A_FaceTarget
    RASK J 9 Bright A_FaceTarget
    RASK K 10 A_CustomMissile("Revrailshot", 60, 0, random(-4,4),0)
    RASK K 10 A_FaceTarget
    Goto See
  Pain:
    RASK L 5
    RASK L 5 A_Pain
    Goto See
  Death:
    RASK LM 7
    RASK N 7 A_Scream
    RASK O 7 A_NoBlocking
    RASK P 7
    RASK Q -1
    Stop
  Raise:
    RASK Q 5
    RASK PONML 5
    Goto See
  }
}

ACTOR Revrailshot : FastProjectile
{
  radius 1
  height 1
  speed 100
  damage (30)
  renderstyle Add
  decal RailScorchLower
  seesound "weapons/railgf"
  deathsound "weapons/railgf"
  obituary "%o was shafted by a Beam Revenant."
  PROJECTILE
  +RANDOMIZE
  +BLOODSPLATTER
   +FOILINVUL
   +DONTREFLECT
  states
  {
  Spawn:
    TNT1 A 1 bright A_SpawnItemEx("railtrail", 0, 0, 0,0,0,0,0,128)
    loop
  Death:
    TNT1 A 2 bright A_SpawnItemEx("railpow", 0, 0, 0,0,0,0,0,128)
    stop
  }
}

ACTOR Railtrail
{
   Radius 2
   Height 2
   Speed 0
   scale 0.5
   PROJECTILE
   +CLIENTSIDEONLY
   +NOINTERACTION
   States
   {
   Spawn:
   APLS ABABAB 3 BRIGHT
   Stop
   }
}

ACTOR Railpow
{
   Radius 2
   Height 2
   Speed 0
   Scale 0.5
   PROJECTILE
   +NOINTERACTION
   +CLIENTSIDEONLY
   States
   {
   Spawn:
     APBX ABCDE 5 bright
     Stop
   }
}
