actor NetherDarkImp 11368
{
	//$Category JonaDoom_Monsters
    Health 300
    Radius 22
    Height 60
    Speed 9
    PainChance 100
    Mass 500
    Scale 1.2
    MONSTER
    +FLOORCLIP
    SeeSound "imp/sight"
    PainSound "imp/pain"
    DeathSound "imp/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o was cursed by a dark imp."
    HitObituary "%o was touched by a dark imp."
    MeleeDamage 6
    MissileType "InfernoBall"
    Missileheight 32
    States
    {
    Spawn:
        DRKF AB 10 A_Look
        Loop
    See:
        DRKF AABBCCDD 3 A_Chase
        Loop
    Melee:
        DRKF IJ 8 A_FaceTarget
        DRKF K 6 A_ComboAttack
        Goto See
    Missile:
        DRKF E 1 A_Jump (255, "Attack1", "Attack2")
        Goto See
    Attack1:
        DRKF EF 8 A_FaceTarget
        DRKF G 6 A_CustomMissile ("NetherLightning", 32, 0, 0)
        Goto See
    Attack2:
        DRKF IJ 8 A_FaceTarget
        DRKF K 6 A_CustomMissile ("InfernoBall", 32, 0, 0)
        Goto See
    Pain:
        DRKF H 2
        DRKF H 2 A_Pain
        Goto See
    Death:
        DRKF L 8
        DRKF M 8 A_Scream
        DRKF N 5
        DRKF O 5 A_Fall
        DRKF P -1
        Stop
    XDeath:
        DRKF Q 5
        DRKF R 5 A_XScream
        DRKF S 5
        DRKF T 5 A_Fall
        DRKF UVW 5
        DRKF X -1
        Stop
    Raise:
        DRKF PO 8
        DRKF NML 6
        Goto See
    }
}

actor InfernoBall
{
    Radius 8
    Height 6
    Speed 20
    Damage 12
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
        IBAL AAAABBBB 1 Bright A_SpawnItemEx ("InfernoBallTrail", 0, 0, 0, random(0, -4), random(-2, 2), random(-2, 2), 0, 0, 0)
        Loop
    Death:
        IBAL CDE 4 Bright
        Stop
    }
}

actor InfernoBallTrail
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
    +NOGRAVITY
    Scale 0.2
    States
    {
    Spawn:
        ITRL ABCDEFGHIJKLMN 1 Bright
        stop
    }
}

actor NetherLightning
{
    Radius 8
    Height 6
    Speed 24
    Damage 4
    PROJECTILE
    +RIPPER
    RENDERSTYLE ADD
    ALPHA 0.75
    Scale 0.75
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal DoomImpScorch
    States
    {
    Spawn:
        NLIT AA 1 Bright A_SpawnItemEx ("NetherLightningTrail", 0, 0, 0, 0, 0, 0, 0, 0, 0)
        NLIT A 0 Bright A_SpawnItemEx ("NetherLightningSpark", 0, 0, 0, random(0, -4), random(-2, 2), random(-2, 2), 0, 0, 0)
        NLIT AA 1 Bright A_SpawnItemEx ("NetherLightningTrail", 0, 0, 0, 0, 0, 0, 0, 0, 0)
        NLIT A 0 Bright A_SpawnItemEx ("NetherLightningSpark", 0, 0, 0, random(0, -4), random(-2, 2), random(-2, 2), 0, 0, 0)
        loop
    Death:
        NLIT ABCDEFG 1 Bright
        Stop
    }
}

actor NetherLightningTrail
{
    Radius 8
    Height 6
    Speed 1
    Damage 0
    PROJECTILE
    +RIPPER
    RENDERSTYLE ADD
    ALPHA 0.75
    Scale 0.75
    States
    {
    Spawn:
        NLIT A 1 A_FadeOut (0.025)
        NLIT A 0 A_Jump (32, "Spark")
        loop
    Spark:
        NLIT A 0 A_SpawnItemEx ("NetherLightningSpark", 0, 0, 0, random(0, -4), random(-2, 2), random(-2, 2), 0, 0, 0)
        goto Spawn
    }
}

actor NetherLightningSpark
{
    Radius 1
    Height 1
    Speed 0
    Damage 0
    +NOGRAVITY
    Scale 0.3
    States
    {
    Spawn:
        NLIT BCDEFG 1 Bright
        stop
    }
}
