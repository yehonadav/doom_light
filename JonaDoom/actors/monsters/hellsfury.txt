Actor HellsFury 3127
{
	//$Category JonaDoom_Monsters
  Health 1000
  Radius 24
  Height 64
  Speed 8
  PainChance 50
  Mass 1000
  MONSTER
  +FLOORCLIP
  SeeSound "baron/sight"
  ActiveSound "baron/active"
  PainSound "baron/pain"
  DeathSound "baron/death"
  Obituary "%o felt the wrath of Hell's Fury"
  HitObituary "%o got ripped apart by Hell's Fury"
  MissileType HellsFuryBall
  MeleeDamage 11
  States
  {
  Spawn:
    HFRY AB 10 A_Look
    Loop
  See:
    HFRY AABBCCDD 4 A_Chase
    Loop
  Missile:
    HFRY E 0 A_Jump(96,4)
    HFRY EF 8 A_FaceTarget
    HFRY G 8 A_ComboAttack
    Goto See
    HFRY P 8 A_FaceTarget
    HFRY Q 0 A_CustomMissile("SoulHarvesterBall", 60, 0, -45)
    HFRY Q 0 A_CustomMissile("SoulHarvesterBall", 60, 0, 0)
    HFRY Q 8 A_CustomMissile("SoulHarvesterBall", 60, 0, 45)
    Goto See
    Melee:
    HFRY EF 8 A_FaceTarget
    HFRY G 8 A_ComboAttack
    Goto See
  Pain:
    HFRY H 2
    HFRY H 2 A_Pain
    Goto See
  Death:
    HFRY I 8 A_Scream
    HFRY J 0 A_FaceTarget
    HFRY J 0 A_Jump(51,5)
    HFRY J 0 A_Jump(51,5)
    HFRY J 0 A_Jump(51,5)
    HFRY J 0 A_Jump(51,5)
    HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, 20, 128)
    Goto Death+11
    HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, 10, 128)
    Goto Death+11
    HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, 0, 128)
    Goto Death+11
    HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, -10, 128)
    Goto Death+11
    HFRY J 0 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, -20, 128)
    Goto Death+11
    HFRY J 8 
    HFRY K 8
    HFRY L 8 A_Fall
    HFRY MN 8
    HFRY O -1
    Stop
  Raise:
    HFRY NMLKJI 8
    Goto See
   }
}

Actor HellsFuryBall 
{
  Radius 6
  Height 8
  Speed 14
  Damage 8
  Projectile
  RenderStyle Add
  Alpha 0.7
  SeeSound "imp/attack"
  DeathSound "imp/shotx"
  Decal BaronScorch
  States
  {
  Spawn:
    HFBB AB 5 Bright
    Loop
  Death:
    HFBB CDE 5 Bright
    Stop
  }
}

Actor SoulHarvesterBall
{
  Radius 8
  Height 6
  Scale 0.4
  Speed 9
  Damage 2
  +SeekerMissile
  Projectile
  RenderStyle Add
  SeeSound "harvester/scream"
  DeathSound "imp/shotx"
  Decal DoomImpScorch
  States
  {
  Spawn:
    SHBA A  0 Bright A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
    SHBA AB 2 Bright A_SeekerMissile(10,20)
    Loop
  Death:
    SHBA CDEFGHIJKL 4 Bright
    Stop
  }
}

Actor HellsFurySkull
{
  Radius 5
  Height 5
  Speed 5
  Mass 5000
  +DropOff
  +DoomBounce
  +Missile
  States
  {
  Spawn:
    FSKL ABCDEFGHIJK 2
    Loop
  Death:
    FSKL L -1
    Loop
  }
}

Actor SoulBallTrail
{
  Radius 1
  Height 1
  Speed 0
  Projectile
  RenderStyle Add
  Alpha 0.5
  +NoClip
  States
  {
  Spawn:
  Death:
    SBTR H 6 
    SBTR ABCDEFG 4 Bright
    Stop
  }
}

