ACTOR PyroZombie 3128
{
	//$Category JonaDoom_Monsters
  obituary "%o was flambeed by a Pyro Zombie."
  health 200
  radius 20
  height 56
  mass 100
  speed 8
  painchance 170
  seesound "chainguy/sight"
  painsound "chainguy/pain"
  deathsound "chainguy/death"
  activesound "chainguy/active"
  missiletype PyroShot
  MONSTER
  +FLOORCLIP
  states
  {
  Spawn:
    PYRZ AB 10 A_Look
    loop
  See:
    PYRZ AABBCCDD 3 A_Chase
    loop
  Missile:
    PYRZ E 5 A_FaceTarget
    PYRZ F 5 Bright
    PYRZ F 0 Bright A_PlaySound ("PyroCannon/Fire")
    PYRZ F 5 Bright A_MissileAttack
    PYRZ E 5
    goto See
  Pain:
    PYRZ G 3
    PYRZ G 3 A_Pain
    goto See
  Death:
    PYRZ H 5
    PYRZ I 5 A_Scream
    PYRZ J 5 A_NoBlocking
    PYRZ KLM 5
    PYRZ N -1
    stop
  XDeath:
    PYRZ O 5
    PYRZ P 5 A_XScream
    PYRZ Q 5 A_NoBlocking
    PYRZ RS 5
    PYRZ T -1
    stop
  Raise:
    PYRZ NMLKJIH 5
    goto See
  }
}

Actor PyroShot
{
  Radius 8
  Height 12
  Speed 30
  Damage (20*Random(12, 16))
  Projectile
  RenderStyle Add
  DamageType Fire
  Alpha 0.95
  Scale 0.75
  SeeSound "PyroCannon/Fire"
  DeathSound "PyroCannon/Hit"
  Decal BigScorch
  States
  {
  Spawn:
    PBAL AAAABBBB 4 Bright A_Explode(10, 128, 0)
    Loop
  Death:
    PYXP AB 2 Bright
    PYXP C 2 Bright A_Explode(192, 128)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 0, 4)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 45, 4)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 90, 4)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 135, 4)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 180, 4)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 225, 4)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 270, 4)
    PYXP C 0 Bright A_CustomMissile("PyroSpawner", 0, 0, 315, 4)
    PYXP C 0 Bright A_Mushroom("PyroFlare", 16)
    PYXP DEFGHIJKLMNOPQRSTU 2 Bright
    Stop
  }
}

Actor PyroSpawner
{
  Radius 1
  Height 1
  Damage (0)
  Projectile
  Speed 15
  +Ripper
  +BloodlessImpact
  States
  {
  Spawn:
    TNT1 AAAAA 5 Bright A_CustomMissile("PyroBoom", 0, 0, 0, 4)
    Stop
  }
}

Actor PyroBoom
{
  Radius 0
  Height 1
  Damage (0)
  Projectile
  RenderStyle Add
  DamageType Fire
  Alpha 0.95
  Scale 0.67
  SeeSound "PyroCannon/Explosion"
  States
  {
  Spawn:
    PYXP AB 2 Bright
    PYXP C 2 Bright A_Explode(192, 128)
    PYXP DEFGHIJKLMNOPQRSTU 2 Bright
    Stop
  }
}

Actor PyroFlare
{
  Radius 8
  Height 11
  Speed 35
  Damage (10*Random(2, 3))
  Projectile
  RenderStyle Add
  DamageType Fire
  Decal DoomImpScorch
  Alpha 0.95
  -NoGravity
  DeathSound "PyroCannon/Flare"
  States
  {
  Spawn:
    TNT1 A 1 Bright A_SpawnItemEx("PyroFX", 0, 0, 0, 0, 0, 0, 0, 128)
    Loop
  Death:
    FRFX HIJ 2 Bright
    FRFX J 0 A_Explode(64, 64)
    FRFX J 0 A_CustomMissile("DropFire", 0, 0, 0, 6)
    FRFX KLMNO 2 Bright
    Stop
  }
}

Actor PyroFX
{
  Radius 0
  Height 1
  Speed 0
  Projectile
  RenderStyle Add
  Alpha 0.85
  States
  {
  Spawn:
    TNT1 A 3 Bright
    PYFX ABCDE 3 Bright
    Stop
  }
}

Actor DropFire
{
  Radius 8
  Height 40
  Projectile
  RenderStyle Add
  DamageType Fire
  Alpha 0.90
  -NoGravity
  +LowGravity
  +NoExplodeFloor
  +NoDamageThrust
  +ThruGhost
  States
  {
  Spawn:
    FLME A 1 Bright A_Playsound("Flamethrower/Flame")
    FLME A 1 Bright A_Explode(5, 16, 0)
    FLME BCDEFGHIJKLMN 2 Bright A_Explode(5, 16, 0)
    FLME A 1 Bright A_Playsound("Flamethrower/Flame")
    FLME A 1 Bright A_Explode(5, 32, 0)
    FLME BCDEFGHIJKLMN 2 Bright A_Explode(5, 16, 0)
    FLME A 1 Bright A_Playsound("Flamethrower/Flame")
    FLME A 1 Bright A_Explode(5, 32, 0)
    FLME BCDEFGHIJKLMN 2 Bright A_Explode(5, 16, 0)
    FLME A 1 Bright A_Playsound("Flamethrower/Flame")
    FLME A 1 Bright A_Explode(5, 32, 0)
    FLME BCDEFGHIJKLMN 2 Bright A_Explode(5, 16, 0)
    FLME A 1 Bright A_Playsound("Flamethrower/Flame")
    FLME A 1 Bright A_Explode(5, 32, 0)
    FLME BCDEFGHIJKLMN 2 Bright A_Explode(5, 16, 0)
    FLME A 0 A_Jump(192, 1)
    Loop
    TNT1 A 0
  Death:
    TNT1 A 1 Bright
    Stop
  }
}
