//Shadow
ACTOR Shadow 3126
{
//$Category JonaDoom_Monsters
	painchance "Stunner", 160

	SpawnID 235
	Health 80
	Radius 20
	Height 56
	Mass 100
	RenderStyle "Translucent"
	Alpha 0.7
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	PainSound "Shadow/pain"
	DeathSound "Shadow/death"
	ActiveSound "Shadow/active"
	Obituary "%o was assassinated by a shadow."
	MissileType ShadowBall
	States
	{
	Spawn:
		SHDW AB 10 A_Look
		Loop
	See:
		SHDW A 15
		SHDW AAA 1 A_Chase
		SHDW A 0 A_SpawnItemEx("ShadowGhostA", 0, 0, 0, 0, 0, 0, 0, 128)
		SHDW BBB 1 A_Chase
		SHDW B 0 A_SpawnItemEx("ShadowGhostB", 0, 0, 0, 0, 0, 0, 0, 128)
		SHDW CCC 1 A_Chase
		SHDW C 0 A_SpawnItemEx("ShadowGhostC", 0, 0, 0, 0, 0, 0, 0, 128)
		SHDW DDD 1 A_Chase
		SHDW D 0 A_SpawnItemEx("ShadowGhostD", 0, 0, 0, 0, 0, 0, 0, 128)
		SHDW AAA 1 A_Chase
		SHDW A 0 A_SpawnItemEx("ShadowGhostA", 0, 0, 0, 0, 0, 0, 0, 128)
		SHDW BBB 1 A_Chase
		SHDW B 0 A_SpawnItemEx("ShadowGhostB", 0, 0, 0, 0, 0, 0, 0, 128)
		SHDW CCC 1 A_Chase
		SHDW C 0 A_SpawnItemEx("ShadowGhostC", 0, 0, 0, 0, 0, 0, 0, 128)
		SHDW DDD 1 A_Chase
		SHDW D 0 A_SpawnItemEx("ShadowGhostD", 0, 0, 0, 0, 0, 0, 0, 128)
		Goto See+1
	Missile:
		SHDW E 30 A_FaceTarget
		SHDW F 5 
		SHDW G 5 BRIGHT A_MissileAttack
		SHDW F 5
		SHDW E 20
		SHDW F 5 
		SHDW G 5 BRIGHT A_MissileAttack
		SHDW F 5
		SHDW E 20
		SHDW F 5 
		SHDW G 5 BRIGHT A_MissileAttack
		SHDW F 5
		SHDW E 20
		Goto See+1      
	Pain:
		SHDW H 4
		SHDW H 4 A_Pain
		Goto See+1
        Pain.Stunner:
                SHDW H 2 A_ChangeFlag("NoPain", 1)
                SHDW H 300 A_Pain
                SHDW H 0 A_ChangeFlag("NoPain", 0)
                goto See+1
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem", 4)
		SHDW I 6 A_Scream
		SHDW J 5
		SHDW K 5 A_SpawnItemEx("Shadowtorso", 0, 0, 0, 1, 0, 0, 0, 128)
		SHDW L 5 A_Fall
		SHDW MNO 6 
		SHDW P 3000
                SHDW PPPPPPPPPPPP 2 A_FadeOut(0.1)
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("AwardItem", 6)
		SHDX A 8
		SHDX B 6 A_XScream
		SHDX C 6 
		SHDX D 6 A_Fall
 		SHDX E 6
 		SHDX F 6
		SHDX G 3000
                SHDX GGGGGGGGGGGG 2 A_FadeOut(0.1)
		Stop
	Raise:
		SHDW M 8
		SHDW KLJI 8
		Goto See
	}
}

ACTOR SuperShadow : Shadow 110
{
    Health 300
    Radius 32
	Height 90
    Speed 11.5
    PainChance 100
    Scale 1.7
    Mass 400
    States
    {
    Missile:
		SHDW E 5 A_FaceTarget
		SHDW F 2
		SHDW G 2 BRIGHT A_MissileAttack
		SHDW F 2
		SHDW E 7
		SHDW F 2
		SHDW G 2 BRIGHT A_MissileAttack
		SHDW F 2
		SHDW E 7
		SHDW F 2
		SHDW G 2 BRIGHT A_MissileAttack
		SHDW F 2
		SHDW E 10
		Goto See+1
    }
}

ACTOR ShadowBall
{
	Radius 6
	Height 8
	Speed 12
	Damage 4
	Projectile
	+RANDOMIZE
	RenderStyle "ADD"
	Alpha 0.75
	SeeSound "Shadow/attack"
	DeathSound "imp/shotx"
	Decal DoomImpScorch
	States
	{
	Spawn: 
		SBAL ABC 4 BRIGHT A_SpawnItemEx("ShadowTrail", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Death:
		SBAL C 5 Bright
		SBAL DEFGH 4 BRIGHT
		Stop
	}
}

ACTOR ShadowGhostA
{
	Radius 4
	Height 8
	Speed 0
	Damage 0
	Mass 75
	RenderStyle "Translucent"
	Alpha 0.3
	PROJECTILE
	States
	{
	Spawn:
		SHDW A 10
		Stop
	}
}

ACTOR ShadowGhostB : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW B 10
		Stop
	}
}

ACTOR ShadowGhostC : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW C 10
		Stop
	}
}

ACTOR ShadowGhostD : ShadowGhostA
{
	States
	{
	Spawn:
		SHDW D 10
		Stop
	}
}

ACTOR Shadowtorso
{
	Radius 12
	Height 15
	Speed 1
	Mass 5000
	RenderStyle "Translucent"
	alpha 0.7
	+Doombounce
      States
	{
	Spawn:
         SHDW QRS 5
         SHDW T 5 A_Fall
         SHDW U 5
         SHDW V 3000
         SHDW VVVVVVVVVVVV 2 A_FadeOut(0.1)
         Stop
	}
}

actor ShadowTrail
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.6
    +NOCLIP
    States
    {
    Spawn:
     SHTR ABCDEF 4 Bright
        Stop       
    }
}

